plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

apply from: '../jacoco.gradle'
apply plugin: 'kotlin-android'
apply from: '../ktlint.gradle'
apply from: '../detekt.gradle'
apply from: '../sonarcube.gradle'

def localPropertiesFile = file(project(':utilsLib').projectDir.path + "/local.properties")
def localProperties = new Properties()

if (!localPropertiesFile.exists()) {
    localProperties.setProperty("APPCENTER_KEY_PRODUCTION", "")
    localProperties.setProperty("APPCENTER_KEY_DEVELOPMENT", "")

    Writer writer = new FileWriter(localPropertiesFile, false)
    localProperties.store(writer, "empty, as creating the file is done manually via gpg")
    writer.close()
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError true
        warningsAsErrors false
        checkAllWarnings = true
        xmlReport false
        htmlReport true
    }

    buildTypes {
        def appcenterKeyProd = System.getenv("APPCENTER_KEY_PRODUCTION")
        if (appcenterKeyProd == null) {
            Properties properties = new Properties()
            properties.load(project.file('local.properties').newDataInputStream())
            appcenterKeyProd = properties.getProperty('APPCENTER_KEY_PRODUCTION')
        }

        def appcenterKeyDev = System.getenv("APPCENTER_KEY_DEVELOPMENT")
        if (appcenterKeyDev == null) {
            Properties properties = new Properties()
            properties.load(project.file('local.properties').newDataInputStream())
            appcenterKeyDev = properties.getProperty('APPCENTER_KEY_DEVELOPMENT')
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "APPCENTER_KEY", "\"$appcenterKeyProd\"")
        }
        debug {
            buildConfigField("String", "APPCENTER_KEY", "\"$appcenterKeyDev\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    flavorDimensions 'Version'
    productFlavors {
        'fdroid' {
            dimension 'Version'
        }
        'googleplay' {
            dimension 'Version'
        }
        'emanuelkebede' {
            dimension 'Version'
        }
    }

    sourceSets {
        fdroid {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
        googleplay {
            java.srcDirs = ['src/main/java', 'src/googleplay/java']
        }
        emanuelkebede {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }
}

dependencies {
    googleplayImplementation platform('com.google.firebase:firebase-bom:27.0.0')
    googleplayImplementation 'com.google.firebase:firebase-analytics'
    googleplayImplementation 'com.google.firebase:firebase-appindexing'
    googleplayImplementation 'com.google.firebase:firebase-perf'
    googleplayImplementation 'com.microsoft.appcenter:appcenter-analytics:4.3.1'
    googleplayImplementation 'com.microsoft.appcenter:appcenter-crashes:4.3.1'

    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.test.ext:junit:1.1.3'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.6.0'
    implementation 'org.robolectric:robolectric:4.7.2'

    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'junit:junit:4.13.2'
}

repositories {
    mavenCentral()
}
