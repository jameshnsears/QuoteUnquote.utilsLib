plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

apply plugin: 'kotlin-android'
apply from: '../detekt.gradle'
apply from: '../jacoco.gradle'
apply from: '../ktlint.gradle'
apply from: '../sonarqube.gradle'

final def localPropertiesFile = file(project(':utilsLib').projectDir.path + "/local.properties")
final def localProperties = new Properties()

if (!localPropertiesFile.exists()) {
    localProperties.setProperty("APPCENTER_KEY_PRODUCTION", "")
    localProperties.setProperty("APPCENTER_KEY_DEVELOPMENT", "")

    final Writer writer = new FileWriter(localPropertiesFile, false)
    localProperties.store(writer, "empty, as creating the file is done manually via gpg")
    writer.close()
}

android {
    compileSdk 36

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        def appCenterKeyProd = System.getenv("APPCENTER_KEY_PRODUCTION")
        if (appCenterKeyProd == null) {
            final Properties properties = new Properties()
            properties.load(project.file('local.properties').newDataInputStream())
            appCenterKeyProd = properties.getProperty('APPCENTER_KEY_PRODUCTION')
        }

        def appCenterKeyDev = System.getenv("APPCENTER_KEY_DEVELOPMENT")
        if (appCenterKeyDev == null) {
            final Properties properties = new Properties()
            properties.load(project.file('local.properties').newDataInputStream())
            appCenterKeyDev = properties.getProperty('APPCENTER_KEY_DEVELOPMENT')
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "APPCENTER_KEY", "\"$appCenterKeyProd\"")
        }

        debug {
            buildConfigField("String", "APPCENTER_KEY", "\"$appCenterKeyDev\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = 17
    }

    flavorDimensions 'Version'
    productFlavors {
        'fdroid' {
            dimension 'Version'
        }
        'googleplay' {
            dimension 'Version'
        }
        'espresso' {
            dimension 'Version'
        }
        'uiautomator' {
            dimension 'Version'
        }
    }

    sourceSets {
        fdroid {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
        googleplay {
            java.srcDirs = ['src/main/java', 'src/googleplay/java']
        }
        espresso {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
        uiautomator {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }

    lint {
        abortOnError true
        checkAllWarnings true
        htmlReport true
        warningsAsErrors false
        xmlReport false
    }

    namespace 'com.github.jameshnsears.quoteunquote.utils'
}

dependencies {
    googleplayImplementation 'com.microsoft.appcenter:appcenter-crashes:5.0.6'
    googleplayImplementation 'com.google.firebase:firebase-crashlytics:20.0.4'

    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    testImplementation 'androidx.test.ext:junit:1.3.0'
    testImplementation 'androidx.test:runner:1.7.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.16.1'
}

repositories {
    mavenCentral()
}
