plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

apply from: '../jacoco.gradle'
apply plugin: 'kotlin-android'
apply from: '../ktlint.gradle'
apply from: '../detekt.gradle'
apply from: '../sonarcube.gradle'

def localPropertiesFile = file(project(':utilsLib').projectDir.path + "/local.properties")
def localProperties = new Properties()

if (!localPropertiesFile.exists()) {
    localProperties.setProperty("APPCENTER_KEY_PRODUCTION", "")
    localProperties.setProperty("APPCENTER_KEY_DEVELOPMENT", "")

    Writer writer = new FileWriter(localPropertiesFile, false)
    localProperties.store(writer, "empty, as creating the file is done manually via gpg")
    writer.close()
}

android {
    compileSdk 33

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        def appcenterKeyProd = System.getenv("APPCENTER_KEY_PRODUCTION")
        if (appcenterKeyProd == null) {
            Properties properties = new Properties()
            properties.load(project.file('local.properties').newDataInputStream())
            appcenterKeyProd = properties.getProperty('APPCENTER_KEY_PRODUCTION')
        }

        def appcenterKeyDev = System.getenv("APPCENTER_KEY_DEVELOPMENT")
        if (appcenterKeyDev == null) {
            Properties properties = new Properties()
            properties.load(project.file('local.properties').newDataInputStream())
            appcenterKeyDev = properties.getProperty('APPCENTER_KEY_DEVELOPMENT')
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "APPCENTER_KEY", "\"$appcenterKeyProd\"")
        }
        debug {
            buildConfigField("String", "APPCENTER_KEY", "\"$appcenterKeyDev\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    flavorDimensions 'Version'
    productFlavors {
        'fdroid' {
            dimension 'Version'
        }
        'googleplay' {
            dimension 'Version'
        }
    }

    sourceSets {
        fdroid {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
        googleplay {
            java.srcDirs = ['src/main/java', 'src/googleplay/java']
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }
    lint {
        abortOnError true
        checkAllWarnings true
        htmlReport true
        warningsAsErrors false
        xmlReport false
    }
    namespace 'com.github.jameshnsears.quoteunquote.utils'
}

dependencies {
    googleplayImplementation platform('com.google.firebase:firebase-bom:30.3.2')
    googleplayImplementation 'com.google.firebase:firebase-analytics:21.1.0'
    googleplayImplementation 'com.google.firebase:firebase-appindexing:20.0.0'
    googleplayImplementation 'com.google.firebase:firebase-perf:20.1.0'
    googleplayImplementation 'com.microsoft.appcenter:appcenter-analytics:4.4.5'
    googleplayImplementation 'com.microsoft.appcenter:appcenter-crashes:4.4.5'

    implementation 'androidx.annotation:annotation:1.4.0'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.2'
}

repositories {
    mavenCentral()
}
